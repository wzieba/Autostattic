initscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }

    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:0.39.0"
        classpath "com.dipien:bye-bye-jetifier:1.2.0"
        classpath "com.vanniktech:gradle-android-junit-jacoco-plugin:0.16.0"
    }
}

rootProject {
    apply plugin: com.github.benmanes.gradle.versions.VersionsPlugin
    apply plugin: com.dipien.byebyejetifier.ByeByeJetifierGradlePlugin
    apply plugin: com.vanniktech.android.junit.jacoco.GenerationPlugin

    tasks.named("dependencyUpdates").configure {
        def isNonStable = { String version ->
            def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { keyword -> version.toUpperCase().contains(keyword) }
            def regex = /^[0-9,.v-]+(-r)?$/
            return !stableKeyword && !(version ==~ regex)
        }

        outputFormatter = "json"
        outputDir = "$rootDir"
        rejectVersionIf {
            isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
        }
    }
}
